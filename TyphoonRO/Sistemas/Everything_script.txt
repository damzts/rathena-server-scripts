// ==== *Athena Script =================================
// Kamishi's Palettes Protection V.1.0
// Never show this file to anyone, except yourself. ;)
// =====================================================

// =====================================================
// Initialize the hash table required to access secret 
// palette file numbers.
// =====================================================

prontera,179,27,5	script	#121345	HIDDEN_NPC,{
	end;
	OnPCLoginEvent:
		if (!setPaletteToProtectedNumber == 2){
			callfunc("setlook2", 6, realPlayerLookHair);
			callfunc("setlook2", 7, realPlayerLookClothes);
			set setPaletteToProtectedNumber,2;
		}
		end;

	OnInit:
		set $paletteSECRET1, 179;
		set $paletteMAGIC2, 11;
		set $paletteSECRET3, 7000;
		set $paletteOFFSET, 600;
		setarray $paletteHash[0],141,145,21,16,168,116,102,78,137,165,8,112,41,75,158,178,97,142,27,106,49,110,37,128,98,164,87,107,47,151,19,69,39,33,125,129,35,12,74,77,159,86,84,143,140,95,157,150,24,93,120,72,124,160,71,64,29,113,126,147,59,52,134,14,73,54,65,17,152,61,174,133,50,131,156,85,46,91,81,136,177,79,43,28,146,53,144,172,161,23,94,48,155,7,68,163,108,3,32,173,99,30,57,121,18,167,123,109,13,62,138,101,36,34,89,166,122,154,127,114,66,22,6,58,119,38,115;
		end;
}


// ====== Access the secret file palette number. =======
// Method replacement for setlook, changelook, getlook.
// ==> setLook2, getLook2, changeLook2
// =====================================================


function	script	getProtectedPal	{
	set @i, getarg(0);
	
	if(@i == 0) return 0;

	set @type, getarg(1);
	if(@type == 7){
		if(@i < 5) return @i; // set 5 to your unprotected clothes palette #.
	}
	if(@type == 6){
		if(@i < 10) return @i; // set 10 to your unprotected hair palette #.
	}

	set @m, @i / 127; 
	set @d, $paletteHash[(@i +($paletteMAGIC2*@m)) % 127] + ($paletteSECRET1* @m);
	set @d, $paletteSECRET3-@d;
	return @d+$paletteOFFSET;
}

function	script	setlook2	{
	//Save current value:
	set @type, getarg(0);
	set @i, getarg(1);
	
	
	if(@type == 6){
		set realPlayerLookHair, @i;		
	
	}else if (@type == 7){
		set realPlayerLookClothes, @i;		
	}else{
		setlook @type, @i;
		return;
	}
	
	setlook @type, callfunc("getProtectedPal", @i, @type);
	
	return;
}

function	script	changelook2	{
	//Save current value:
	set @type, getarg(0);
	set @i, getarg(1);
	
	
	if((@type == 6) || (@type == 7)){
		changelook @type, callfunc("getProtectedPal", @i, @type);
	}else{
		changelook @type, @i;
	}
	
	
	return;
}

function	script	getlook2	{
	set @type, getarg(0);
	
	if(@type == 6){
		return realPlayerLookHair;		
	}else if (@type == 7){
		return realPlayerLookClothes;		
	}else{
		return getlook(@type);
	}
}

		